plugins {
	id 'org.quiltmc.loom' version '0.12.23'
	id 'maven-publish'
}

group = maven_group
version = project.version
archivesBaseName = archives_base_name

repositories {
	mavenCentral()
	maven { url 'https://maven.terraformersmc.com/releases' }
	maven { url 'https://maven.quiltmc.org/repository/release' }
	maven {
		url 'https://maven.quiltmc.org/repository/snapshot'
		content {
			includeGroup 'org.quiltmc.fabric_api_qsl'
			includeGroupByRegex 'org\\.quiltmc\\.qsl(?:\\.[a-z_]+)?'
		}
	}
}

dependencies {
	minecraft "com.mojang:minecraft:$minecraft_version"
	mappings( loom.layered {
		addLayer quiltMappings.mappings( "org.quiltmc:quilt-mappings:$minecraft_version+build.$quilt_mappings:v2" )
	})
	modImplementation "org.quiltmc:quilt-loader:$loader_version"

	modImplementation "org.quiltmc.quilted-fabric-api:quilted-fabric-api:$quilted_fapi_version-$minecraft_version"

	modImplementation("com.terraformersmc:modmenu:$modmenu_version") {
		transitive false
	}

	implementation "io.github.spair:imgui-java-binding:$imgui_version"
	implementation( "io.github.spair:imgui-java-lwjgl3:$imgui_version" )
	implementation "io.github.spair:imgui-java-natives-windows:$imgui_version"
}

processResources {
	inputs.property 'version', version
	inputs.property 'maven_group', maven_group
	inputs.property 'loader_version', loader_version
	inputs.property 'minecraft_version', minecraft_version

	filesMatching('quilt.mod.json') {
		expand([
			'version': version,
			'maven_group': maven_group,
			'loader_version': loader_version,
			'minecraft_version': minecraft_version,
		])
	}
}

tasks.withType(JavaCompile).configureEach {
	it.sourceCompatibility = 17 // for the IDE support
	it.options.encoding = 'UTF-8'
	it.options.release.set(17)
}

java {
	// if this mod is gonna be a library, its better to generate javadocs too, uncomment this line to generate them
//	withJavadocJar()
	withSourcesJar()
}

jar {
	from('LICENSE') {
		rename { "$it_$archivesBaseName"}
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			group 'io.github.thecodecrafters'
			from components.java
		}
	}

	repositories {
		 mavenLocal()
	}
}
